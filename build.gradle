plugins {
    id 'java'
    id 'war'
    id 'org.owasp.dependencycheck' version '7.1.2'
    id 'org.sonarqube' version '3.4.0.2513'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

sourceSets {
    main {
        java {
            srcDir file('src/main')
        }
    }
    test {
        java {
            srcDir file('src/test')
            compileClasspath += main.output
        }
    }
    smokeTest {
        java {
            srcDir file('src/smokeTest')
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
        resources.srcDir file('src/smokeTest/resources')
    }
    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task functional(type: Test) {
    description = "Runs functional tests"
    group = "Verification"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test) {
    description = "Runs smoke tests"
    group = "Verification"
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    failFast = true
}

tasks.withType(Copy) {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
    smokeTestImplementation.extendsFrom testImplementation
    smokeTestRuntimeOnly.extendsFrom runtimeOnly
    runtimeDependency.extendsFrom runtimeOnly
    implementationDependency.extendsFrom implementation

    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom runtimeOnly
}

task showConfigs {
    configurations.each { println it.name }
}

task copyToLibRuntime(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtimeDependency
}

task copyToLibImplementation(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.implementationDependency
}

task copyMissingLibs(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from('WEB-INF/classes/lib')
    into "$buildDir/output/lib"

    from('WEB-INF/lib')
    into "$buildDir/output/lib"

}

// dependencyCheckAggregate run by HMCTS common pipeline
// Added initial errors to the config/owasp/suppressions.xml file
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
  failBuildOnCVSS = 0

  skipConfigurations = [
    "checkstyle",
    "compileOnly",
    "pmd",
    "integrationTest",
    "functionalTest",
    "smokeTest",
    "contractTestRuntimeClasspath",
    "contractTestCompileClasspath"
  ]
  //skip=true
}

// sonarqube task run by HMCTS common pipeline
project.tasks['sonarqube'].dependsOn test
sonarqube {
  properties {
    property "sonar.projectName", "HMCTS :: lgy-iac-web"
    property "sonar.projectKey", "com.github.hmcts:lgy-iac-web"
    property "sonar.tests", "src/test"
    property "sonar.exclusions", ["**/MOJICT/**", "**/spy/**"]
  }
}

dependencies {

    implementation group: 'log4j', name: 'log4j', version: '1.2.17'
    implementation group: 'struts', name: 'struts-legacy', version: '1.1'
    implementation group: 'struts', name: 'struts', version: '1.1-rc2'
    implementation group: 'servletapi', name: 'servlet-api', version: '2.4-20040521'
    implementation group: 'com.lowagie', name: 'itext', version: '1.4.8'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.4'
    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '2.0.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.1.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.1.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.1.3'
    implementation group: 'javax.mail', name: 'mail', version: '1.4.7'    // 1.3.3 not found in mvncentral
    implementation group: 'org.jdom', name: 'jdom', version: '1.1.3'    // 1.0 not on mvncentral V
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.1.3'
    implementation group: 'quartz', name: 'quartz', version: '1.5.2'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.7.0' // V

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.6.1'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.12.16'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.12.16'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.2'

    testImplementation group: 'commons-io', name: 'commons-io', version: '2.1'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.53.1'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '2.53.1'
    testImplementation group: 'org.apache.poi', name: 'poi', version: '3.9'
    testImplementation group: 'org.dbunit', name: 'dbunit', version: '2.4.8'
    testImplementation group: 'strutstestcase', name: 'strutstestcase', version: '2.1.4-1.2-2.4'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.1.1'

    //smokeTestImplementation sourceSets.main.runtimeClasspath
    //smokeTestImplementation sourceSets.test.runtimeClasspath
    //smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.1.1'
    smokeTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.0'
    smokeTestImplementation group: 'org.springframework', name: 'spring-beans', version: '5.3.22'
    smokeTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.7.3'

    functionalTestImplementation group: 'org.springframework', name: 'spring-beans', version: '5.3.22'
    functionalTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.7.3'

  //  This is the start of identifyiing the runtime dependency libraries
  //  Use the ones from the ant build which are stored in WEB-INF lib and classes/lib
  // until we can complete this exercise
//    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.4.1'
//    runtimeOnly group: 'commons-digester', name: 'commons-digester', version: '1.2'
//    runtimeOnly group: 'commons-collections', name: 'commons-collections', version: '2.1.1'
//    runtimeOnly group: 'commons-pool', name: 'commons-pool', version: '1.2'
//    runtimeOnly group: 'displaytag', name: 'displaytag', version: '1.1'
//    runtimeOnly group: 'ehcache', name: 'ehcache', version: '1.1'
//    runtimeOnly files(
//            'lib/jgs-quartz-0.2.1-dev.jar',
//            'lib/jgs-struts-0.3-dev.jar',
//            'lib/xml-apis.jar',
//    )

}
repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

// Create second war file which will act as the "/health" path
// which is invoked as part of the Common Pipeline as part of the Deploy Dev stage
// The war file only contains a default health.html which responds with a 200 status code
task createHealthWar(type: War) {
  destinationDirectory = file('deploy')
  archiveFileName = 'health.war'
  from('health') {
    include('health.html')
  }
  from('health') {
    include('web.xml')
    into('WEB-INF')
  }
  from('health') {
    include('liveness.html')
    into('liveness')
  }
}

war.dependsOn(copyMissingLibs, createHealthWar)
war {

    destinationDirectory = file('deploy')
    archiveFileName = 'IACFees.war'

    from('common') {
        into('common')
    }
    from('css') {
        into('css')
    }
    from('images') {
        into('images')
    }
    from('js') {
        into('js')
    }
    from('templates') {
        into('templates')
    }
    from('tiles') {
        into('tiles')
    }
    from('WEB-INF') {
        exclude('classes')
        exclude('lib')
        include('tld/**')
        include('*.xml')
        include('*.dtd')
        into('WEB-INF')
    }
    from('WEB-INF') {
        include('classes/log4j.properties')
        include('classes/com/MOJICT/IACFee/config/config.properties')
        into('WEB-INF')
    }
    from("$buildDir/output") {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        include('lib/*.jar')
        into('WEB-INF')
    }
    from('.') {
        include('awsps.sh')
        include('error.jsp')
        include('index.jsp')
        include('version.txt')
    }
}



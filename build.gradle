plugins {
    id 'java'
    id 'war'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

sourceSets {
    main {
        java {
            srcDir file('src/main')
        }
    }
    test {
        java {
            srcDir file('src/test')
            compileClasspath += main.output
        }
    }
    smokeTest {
        java {
            srcDir file('src/smokeTest')
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
        resources.srcDir file('src/smokeTest/resources')
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task smoke(type: Test) {
    description = "Runs smoke tests"
    group = "Verification"
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    failFast = true
}

configurations {
    smokeTestImplementation.extendsFrom testImplementation
    smokeTestRuntimeOnly.extendsFrom runtimeOnly
    runtimeDependency.extendsFrom runtimeOnly
    implementationDependency.extendsFrom implementation
}

task showConfigs {
    configurations.each { println it.name }
}

task copyToLibRuntime(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtimeDependency
}

task copyToLibImplementation(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.implementationDependency
}

task copyMissingLibs(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from('WEB-INF/classes/lib')
    into "$buildDir/output/lib"

    from('WEB-INF/lib')
    into "$buildDir/output/lib"

}

dependencyCheck {
  skip=true
}

dependencies {

    implementation group: 'log4j', name: 'log4j', version: '1.2.16'
    implementation group: 'struts', name: 'struts-legacy', version: '1.1'
    implementation group: 'struts', name: 'struts', version: '1.1-rc2'
    implementation group: 'servletapi', name: 'servlet-api', version: '2.4'
    implementation group: 'com.lowagie', name: 'itext', version: '1.4.8'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.4'
    implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '2.0.1'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.1.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.1.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.1.3'
    implementation group: 'javax.mail', name: 'mail', version: '1.4'    // 1.3.3 not found in mvncentral
    implementation group: 'org.jdom', name: 'jdom', version: '1.1.3'      // 1.0 not on mvncentral V
    //implementation files('lib/jdom.jar')
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.1.3'
    implementation group: 'quartz', name: 'quartz', version: '1.5.2'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.7.0' // V

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.6.1'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.12.10'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.12.10'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.2'

    testImplementation group: 'commons-io', name: 'commons-io', version: '2.1'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.53.1'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '2.53.1'
    testImplementation group: 'org.apache.poi', name: 'poi', version: '3.9'
    testImplementation group: 'org.dbunit', name: 'dbunit', version: '2.4.8'
    testImplementation group: 'strutstestcase', name: 'strutstestcase', version: '2.1.4-1.2-2.4'

    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.4.1'
    runtimeOnly group: 'commons-digester', name: 'commons-digester', version: '1.2'
    runtimeOnly group: 'commons-collections', name: 'commons-collections', version: '2.1.1'
    runtimeOnly group: 'commons-pool', name: 'commons-pool', version: '1.2'
    runtimeOnly group: 'displaytag', name: 'displaytag', version: '1.1'
    runtimeOnly group: 'ehcache', name: 'ehcache', version: '1.1'
    runtimeOnly files(
            'lib/jgs-quartz-0.2.1-dev.jar',
            'lib/jgs-struts-0.3-dev.jar',
            'lib/xml-apis.jar',
    )

}
repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

war.dependsOn(copyToLibRuntime,copyToLibImplementation,copyMissingLibs)
war {

    destinationDirectory = file('deploy')
    archiveFileName = 'IACFees.war'

    from('common') {
        into('common')
    }
    from('css') {
        into('css')
    }
    from('images') {
        into('images')
    }
    from('js') {
        into('js')
    }
    from('templates') {
        into('templates')
    }
    from('tiles') {
        into('tiles')
    }
    from('WEB-INF') {
        exclude('classes')
        exclude('lib')
        include('tld/**')
        include('*.xml')
        include('*.dtd')
        into('WEB-INF')
    }
    from('WEB-INF') {
        include('classes/log4j.properties')
        include('classes/com/MOJICT/IACFee/config/config.properties')
        into('WEB-INF')
    }
    from("$buildDir/output") {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        include('lib/*.jar')
        into('WEB-INF')
    }
    from('.') {
        include('awsps.sh')
        include('error.jsp')
        include('index.jsp')
        include('version.txt')
    }
}
